/- minimalGame.dog

LinuxBuild: Platform='Linux' CPU='amd64' Lang='CPP' optimize='speed';
/-SwingBuild: Platform='Java' CPU='JavaVM' Lang='Java' optimize='speed';
/-AndroidBuild: Platform='Android' CPU='JavaVM' Lang='Java' optimize='power';
/-iPhoneBuild: Platform='IOS' CPU='amd64' Lang='Swift' optimize='speed';

Title = "minimalGame"
FileName = "minimalGame"
Version = "1.0"
CopyrightMesg = "Public Domain"
Authors = "Bruce Long"
Description = "This is an example of a very minimal game"
ProgramOrLibrary = "program"

featuresNeeded = [GameToolkit, Logger]

LicenseText = `Public Domain`

runCode=`
    their GUI_ctxt: cr <- initializeGraphics()
    setUpGame()
    gameLooper.play(cr, 1)
    deinitGraphics()
`
/-//////////   A p p l i c a t i o n   a n d   G l o b a l s
struct GameMenuScreen: inherits=GameMode{
    void: update() <- {}
    void: draw(their GUI_ctxt: cr, me double: interpolation) <- {
        cr.setColor(Styler.DarkSlateBlue)
        cr.rectangle(0,0,screenWidth, screenHeight)
        cr.fillNow()

        cr.setColor(Styler.White)
        our fontSpec:: testFont <- ("Ariel", 20, 0)
        renderText(cr, "Press Space to Play!", testFont, 150, 100)
        renderText(cr, "Press Escape to Exit!", testFont, 150, 200)
        cr.strokeNow()
        window.requestRedraw()
    }
    void: pause() <- {}
    void: resume() <- {}
    void: keyDownEventHandler(their KeyInfo: event) <- {
        switch (event.sym){
            case SDLK_SPACE:{
                gameLooper.setGameMode(gamePlayScreen)
            }
            case SDLK_ESCAPE:{
                gameLooper.quitGame()
            }
        }
    }
    void: init(our GameLooper: ourLooper) <- {gameLooper <- ourLooper}
    void: deInit() <- {}
}
struct GamePlayScreen: inherits=GameMode{
    void: update() <- {}
    void: draw(their GUI_ctxt: cr, me double: interpolation) <- {
        displayImage(cr, "assets/level1Background.png", 0, 0, 1)
        cr.setColor(Styler.White)
        our fontSpec:: testFont <- ("Ariel", 20, 0)
        renderText(cr, "GamePlayScreen!", testFont, 150, 100)
        cr.strokeNow()
        window.requestRedraw()
    }
    void: pause() <- {}
    void: resume() <- {}
    void: keyDownEventHandler(their KeyInfo: event) <- {
        switch (event.sym){
            case SDLK_SPACE:{}
            case SDLK_ESCAPE:{
                pause()
                gameLooper.setGameMode(gameMenuScreen)
            }
        }
    }
    void: init(our GameLooper: ourLooper) <- {looper <- ourLooper}
    void: deInit() <- {}
}
struct GLOBAL{
    their GUI_canvas: drawing_area
    me int: fontSize
    me GameLooper: gameLooper
    our GameMenuScreen:: gameMenuScreen
    our GamePlayScreen:: gamePlayScreen
    void: setUpGame() <- {
        gameLooper.setGameMode(gameMenuScreen)
    }
}

struct APP{
    my GUI: gui
}
