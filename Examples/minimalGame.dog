// minimalGame.dog

LinuxBuild: Platform='Linux' CPU='amd64' Lang='CPP' optimize='speed';
//SwingBuild: Platform='Java' CPU='JavaVM' Lang='Java' optimize='speed';
//AndroidBuild: Platform='Android' CPU='JavaVM' Lang='Java' optimize='power';
//iPhoneBuild: Platform='IOS' CPU='amd64' Lang='Swift' optimize='speed';

Title = "minimalGame"
FileName = "minimalGame"
Version = "1.0"
CopyrightMesg = "Public Domain"
Authors = "Bruce Long"
Description = "This is an example of a very minimal game"
ProgramOrLibrary = "program"

featuresNeeded = [GameToolkit, Logger]

LicenseText = `Public Domain`

runCode=`
    initializeGraphics(true)
    setUpGame()
    gameLooper.play(1)
    deinitGraphics()
`
////////////   A p p l i c a t i o n   a n d   G l o b a l s
struct AlienShip{
    me double: x
    me double: y
}
struct AlienArmada{
    our AlienShip[list]: aliens
    me int: alien1Width
    me int: alien1Height
    me int: leftMostPos
    me int: rightMostPos
    me int: alienShotOdds
    me mode [goingLeft, goingRight]: direction
    me GameImage: alien1Img

    void: update() <- {
        leftMostPos  <- screenWidth
        rightMostPos <- 0
        me double: xToMove
        me double: yToMove <- 0
        withEach alien in aliens{
            if(alien.x > rightMostPos){
                rightMostPos <- alien.x
            }
            if(alien.x < leftMostPos){
                leftMostPos  <- alien.x
            }
        }
        if(direction == goingLeft){
            if(leftMostPos < 10){
                direction <- goingRight
                yToMove   <- 30

            }
        }else if(direction == goingRight){
            if(rightMostPos > screenWidth - alien1Width - 10){
                direction <- goingLeft
                yToMove   <- 30
            }
        }
        if(direction == goingLeft){
            xToMove <- -0.3
        }else if(direction == goingRight){
            xToMove <- 0.3
        }
        withEach ship in aliens{
            ship.x <- ship.x + xToMove
            ship.y <- ship.y + yToMove
	    if (randInt(alienShotOdds) == 0) {
		our Bullet:: newBullet
		newBullet.x <- ship.x + alien1Width/2
		newBullet.y <- ship.y + alien1Height
		newBullet.owner <- theirBullet
		gamePlayScreen.bullets.pushFirst(newBullet)
		// playerFires.play()		// TODO: give aliens their own firing sound
	    }
        }
    }
    void: draw(their GUI_ctxt: cr) <- {
        withEach alien in aliens{
            alien1Img.draw(cr, alien.x, alien.y, scaleFactor)
        }
    }
    void: addAlienRow(me int: yPos, me int: numAliens)<-{
        me int: rowWidth <- numAliens * (alien1Width + 15)
        me int: rowStart; rowStart <- (screenWidth - rowWidth) / 2
        me int: crntPos  <- rowStart
        withEach pos in RANGE (0..numAliens){
            our AlienShip:: alien
            alien.x <- crntPos
            alien.y <- yPos
            crntPos <- crntPos + alien1Width + 15
            aliens.pushLast(alien)
        }
    }
    void: addAliens(me int: level)<-{
        if(level == 1){
            addAlienRow(10, 5)
            addAlienRow(100, 6)
        }
    }
    void: init() <-{
        alien1Img.loadFromFile("assets/greenAlien.png")
        alien1Width  <- alien1Img.width/scaleFactor
        alien1Height <- alien1Img.height/scaleFactor
        addAliens(1)
	alienShotOdds <- 1000  // the lower the number, the more they shoot
    }
}
struct Bullet{
    me int: x
    me int: y
    mode[ourBullet, theirBullet]: owner

    me bool: update() <- {
        me bool: deleteIt <- false
        if(owner == ourBullet){
            y <- y - 1
        }
        else if(owner == theirBullet){
            y <- y + 1
        }
        if(y < 0 or y > screenHeight){
            deleteIt <- true
        }
        return(deleteIt)
    }
    me bool: draw(their GUI_ctxt: cr) <- {
        if(owner == ourBullet){
            cr.setColor(Styler.White)
            cr.setLineWidth(4)
            cr.moveTo(x, y)
            cr.lineTo(x, y+10)
        } else if(owner == theirBullet) {
	    cr.setColor(Styler.Red)
            cr.setLineWidth(4)
            cr.moveTo(x, y)
            cr.lineTo(x, y-10)
	}
        cr.strokeNow()
    }
}
struct GameMenuScreen: inherits=GameMode{
    void: update() <- {}
    void: draw(their GUI_ctxt: cr, me double: interpolation) <- {
        cr.setColor(Styler.DarkSlateBlue)
        cr.rectangle(0,0,screenWidth, screenHeight)
        cr.fillNow()

        cr.setColor(Styler.White)
        our fontSpec:: testFont <- ("Ariel", 20, 0)
        renderText(cr, "Press Space to Play!", testFont, 150, 100)
        renderText(cr, "Press Escape to Exit!", testFont, 150, 200)
        cr.strokeNow()
        window.requestRedraw()
    }
    void: pause() <- {}
    void: resume() <- {}
    void: keyDownEventHandler(their KeyInfo: event) <- {
        switch (event.sym){
            case SDLK_SPACE:{
                looper.setGameMode(gamePlayScreen)
            }
            case SDLK_ESCAPE:{
                looper.quitGame()
            }
        }
    }
    void: init(our GameLooper: ourLooper) <- {looper <- ourLooper}
    void: deInit() <- {}
}
struct GamePlayScreen: inherits=GameMode{
    me GameSound: playerFires
    me GameSound: explosion1
    me int: playerPosition
    me int: playerMovement
    me int: shipWidth
    me int: shipOffsetY
    our Bullet[list]: bullets
    me AlienArmada: armada

    void: update() <- {
        me int: bulletIdx <- 0
        while (bulletIdx < bullets.size()){
            our Bullet: bullet <- bullets[bulletIdx]
            if(bullet.update()){
                bullets.DeleteNth(bulletIdx)
            }else{
                bulletIdx <- bulletIdx + 1
            }
        }
        armada.update()
        detectCollisions()
    }
    void: draw(their GUI_ctxt: cr, me double: interpolation) <- {
        displayImage(cr, "assets/level1Background.png", 0, 0, 1)
        displayImage(cr, "assets/ship1.png", playerPosition-shipWidth/2, screenHeight-shipOffsetY, scaleFactor)
        armada.draw(cr)
        withEach bullet in bullets{
            bullet.draw(cr)
        }
        cr.setColor(Styler.White)
        our fontSpec:: scoreFont <- ("Ariel", 20, 0)
        renderText(cr, "Score: " + toString(playerScore), scoreFont, 10, 40)
        window.requestRedraw()
    }
    bool: pointIsInRectangle(me int: ptX, me int: ptY, me int: RectX, me int: rectY, me int: rectHeight, me int: rectWidth)<- {
        return ((ptX >= RectX) and (ptX <= RectX + rectWidth) and (ptY >= rectY) and (ptY <= rectY + rectHeight))
    }
    void: detectCollisions() <- {
        me int: bulletIdx <- 0
        while (bulletIdx < bullets.size()){
            our Bullet: bullet <- bullets[bulletIdx]
            if(bullet.owner == ourBullet){
                me int: alienIdx <- 0
                while(alienIdx < armada.aliens.size()){
                    our AlienShip: alien <- armada.aliens[alienIdx]
                    if(pointIsInRectangle(bullet.x, bullet.y, alien.x, alien.y, armada.alien1Height, armada.alien1Width)){
                        explosion1.play()
                        bullets.DeleteNth(bulletIdx)
                        armada.aliens.DeleteNth(alienIdx)
                        playerScore <- playerScore + 1
                        bulletIdx <- bulletIdx - 1
                        alienIdx  <- alienIdx  - 1
                    }
                    alienIdx  <- alienIdx  + 1
                }
            }else if(bullet.owner == theirBullet){
		// TODO: collision with the protaganist
            }
            bulletIdx <- bulletIdx + 1
        }
    }
    void: pause() <- {}
    void: resume() <- {}
    void: keyDownEventHandler(their KeyInfo: event) <- {
        switch (event.sym){
            case SDLK_SPACE:{
                our Bullet:: newBullet
                newBullet.x <- playerPosition
                newBullet.y <- screenHeight - shipOffsetY
                newBullet.owner <- ourBullet
                bullets.pushFirst(newBullet)
                playerFires.play()
            }
            case SDLK_ESCAPE:{
                pause()
                looper.setGameMode(gameMenuScreen)
            }
            case SDLK_LEFT:{
                playerPosition <- max(shipWidth/2, playerPosition - playerMovement)
            }
            case SDLK_RIGHT:{
                playerPosition <- min(screenWidth-shipWidth/2, playerPosition + playerMovement)
            }
            case SDLK_f:{toggleFullScreen()}
        }
    }
    void: init(our GameLooper: ourLooper) <- {
        looper <- ourLooper
        playerPosition <- screenWidth/2
        shipWidth      <- 136/scaleFactor
	playerMovement <- 10
	shipOffsetY <- 100
        playerFires.loadFromFile("assets/fire1.wav")
        explosion1.loadFromFile("assets/explosion1.wav")
        armada.init()
    }
    void: deInit() <- {}
}
struct GLOBAL{
    their GUI_canvas: drawing_area
    const int: scaleFactor    <- 2
    me int: fontSize
    our GameLooper:: gameLooper
    our GameMenuScreen:: gameMenuScreen
    our GamePlayScreen:: gamePlayScreen
    me int: playerScore
    void: setUpGame() <- {
        playerScore <- 0
        gameMenuScreen.init(gameLooper)
        gamePlayScreen.init(gameLooper)
        gameLooper.setGameMode(gameMenuScreen)
    }
    void: toggleFullScreen() <- {
        if(getFullScreenMode()){
            fullScreenModeOff()
        }else{
            fullScreenModeOn()
        }
    }
}

struct APP{
    my GUI: gui
}

// TODO:
// everything moves faster
// detect when aliens hit the ship
// detect lose/win
// aliens have their own firing sound
// clean opening screen
// Music playing
// Mouse Events
// Animations
// ANDROID
// * Multiple Levels
// * Multiple lives
