/- CodeDog GUI Dashoard

/- Widgets for constructing a dashboard

featuresNeeded = [GUI_ToolKit]


struct dashboard: inherits = 'widget'{
    me int: extX        /- right-most extent
    me int: extY        /- lowest extent
    our dash[list]: children
    our dash[map string]: dependentItems
    our decor[list]: decorations


    our dash: dependentIsRegistered(me string: key) <- {
        if(dependentItems.containsKey(key)){
            return(dependentItems.get(key))
        }
        return(NULL)
    }

    me void: addDependent(me string: key, our dash: dashPtr) <- {
        if(! dependentItems.containsKey(key)){
            dashPtr.isHidden <- true
            dependentItems[key] <- dashPtr
        }
    }


    me bool: primaryClick(their GUI_ButtonEvent: event) <- {
        if(! isTouchingMe(event.x, event.y)){return(false)}
        withEach Dash in children:{
            Dash.primaryClick(event)
        }
        withEach Dash in dependentItems:{
            if(! Dash.isHidden){
                Dash.primaryClick(event)
            }
        }
        setPos(0, 0, 0)
        markDirtyArea(drawing_area, 0,0, widgetWidth(thisApp.scrollerRef), widgetHeight(thisApp.scrollerRef))
        return(true)
    }

    me void: drawToPDF(me string: filename, me double: inchesWide, me double: inchesHigh, me double: scale) <- {
  /*      me GUI_ctxt: PDF <- gui_tk.newPDF_ctxt(filename, inchesWide, inchesHigh)
        PDF.setScale(scale, scale)
        draw(PDF)
        PDF.finishPDF()*/
    }

    me bool: secondaryClick(their GUI_PointerEvent: event) <- {print("RIGHT BUTTON!\n"); drawToPDF('infonView.pdf', 11.0, 8.5, 0.4); return(true)}

    void: update() <- {
    }

    void: setPos(me int:x, me int:y, me int: extCursor) <- {
        decorations.clear()
        width <- 10
        height <- 10
        withEach Dash in dependentItems:{
            Dash.isHidden<-true
        }
        withEach Dash in children:{
            extY <- Dash.posY+25
            Dash.setPos(Dash.posX, Dash.posY, extY)
            width<-max(width, Dash.extX)
            height<-max(height, Dash.height+y)
        }
        withEach depDash in dependentItems:{
            if(!depDash.isHidden){
                width<-max(width, depDash.extX)
                height<-max(height, depDash.extY)
            }
        }
        width <- width +30
        height <- height +30
    }

    void: draw(me GUI_ctxt: cr) <- {
        if (isHidden){return()}
        cr.setRGB(255, 255, 255)
        cr.paintNow()

     /-   cr.moveTo(0,0); cr.lineTo(width, height); cr.setRGBA(255, 50, 50, 255); cr.strokeNow();

        cr.setRGBA(0, 0, 0, 255)
        cr.setLineWidth(2)
        withEach Dash in children:{
            Dash.draw(cr)
        }
        withEach depDash in dependentItems:{
            if(!depDash.isHidden){
                depDash.draw(cr)
            }
        }
        withEach decoration in decorations:{
            decoration.draw(cr)
        }
    }
}

struct dash: inherits = 'widget'{
    me int: extX        /- Dashes right-most extent
    me int: extY        /- Dashes lowest extent
    me int: extC        /- The current position where the next external item should be drawn
    me bool: isNullLike
    void: activatePropertyEditor()<- {}
}

struct dataField: inherits = 'dash' {
    me string: label
    me string: value
    me int: midPos
    void: update(me int:MidPos, me int:minWidth, me string: Label, me string: Value, me bool: hideThis) <- {
        isNullLike<-hideThis
        midPos<-MidPos; label<-Label; value<-Value;
    }

   void: setPos(me int:x, me int:y, me int: extCursor) <- {
        posX <- x;
        posY <- y;
        extC <- extCursor
    /-    me deltaSize: DS <- getTextSize(me GUI_ctxt: cr, me string: text, me string: fontName, me int: fontSize)
        height <- 15
        width <- 150
        extX <- posX+width
        extY <- posY+height
    }

    void: draw(me GUI_ctxt: cr) <- {
        if (isHidden){return()}
        renderText(cr, label, "Ariel",  fontSize, posX, posY+15)
        renderText(cr, value, "Ariel",  fontSize, posX+midPos, posY+15)
    }
}

struct ptrToItem: inherits = 'dash' {
    me dataField: header
    me bool: refHidden
    me string: symbol
    our dash: dashPtr

    void: update(me string: Label, me string: textValue, me bool: hideThis) <- {
        isNullLike<-hideThis
        symbol <- textValue
        refHidden <- true
        dashPtr<-dashBoard.dependentIsRegistered(textValue)
        header.update(90, 150, Label, textValue, false)
    }

    void: setPos(me int:x, me int:y, me int: extCursor) <- {
        posX <- x;
        posY <- y;
        extC <- extCursor
        header.setPos(x, y, extY)
        height <- header.height
        width <- header.width
        dashPtr <- dashBoard.dependentIsRegistered(symbol)
        if((!refHidden) and dashPtr!=NULL and dashPtr.isHidden){
            dashPtr.isHidden<-false
        }
        extX <- posX+width
        extY <- posY+height
    }

    me bool: primaryClick(their GUI_ButtonEvent: event) <- {
        if(!isTouchingMe(event.x, event.y)){return(false)}
        if(refHidden){refHidden<-false}
        else {refHidden<-true}
        return(true)
    }

    void: draw(me GUI_ctxt: cr) <- {
        if (isHidden){return()}
        header.draw(cr)

    }
}


struct listOfItems: inherits = 'dash' {
    me mode[headerOnly, fullDisplay, noZeros]: displayMode
    me mode[vertical, horizontal]: orientation
    me dataField: header
    our dash[list]: elements

    void: update(me string: Label, me string: textValue, me bool: hideThis) <- {
        isNullLike<-hideThis
        header.update(90, 150, Label, textValue, false)
        elements.clear()
        displayMode<-headerOnly
    }

    void: updatePush(our dash: element) <- {
        elements.pushLast(element)
    }

    void: setPos(me int:x, me int:y, me int: extCursor) <- {
        posX <- x;
        posY <- y;
        extC <- extCursor
        header.setPos(x, y, y)
        y <- y+header.height
        width <- header.width
        height <- y-posY
        extX <- header.extX
        extY <- max(y, extC)
        if(displayMode!=headerOnly){
            if(orientation==vertical){
                x <- x+8
                withEach element in elements:{
                    element.setPos(x,y,extC)
                    y <- y+element.height
                    width <- max(width, element.width+8)
                    extX <- max(extX, element.extX)
                    extY <- max(extY, element.extY)
                }
                height <- y-posY
            } else if(orientation==horizontal){
                me int: listX <- posX + header.width+30    /- This 30 should really be calculated to compensate for nested items. Actual distance should be 40.
                me int: listY <- extC
                me int: listC <- extC+40;
                withEach element in elements:{
                    element.isHidden <- false
                    element.setPos(listX,listY,listC)
                    extC  <- max(extC, element.extY)
                    extY  <- max(extY, extC)
                    listX <- element.extX + 20
                    extX  <- element.extX
                }
                if(extC > listC){
                    extC <- extC+40
                    extY <- max(extY, extC)
                }
            }
        }
    }

   me bool: primaryClick(their GUI_ButtonEvent: event) <- {
        me int: eventX <- event.x
        me int: eventY <- event.y
     /-   if(!isTouchingMe(eventX, eventY)){return(false)}
        if( header.isTouchingMe(eventX, eventY)){
            if(displayMode==headerOnly){displayMode <- fullDisplay; orientation <- vertical}
            else if(displayMode==fullDisplay and orientation==vertical){
                orientation<-horizontal
            } else {displayMode <- headerOnly}
        } else {
            withEach element in elements:{
                element.primaryClick(event)
            }
        }
        return(true)
    }

    void: draw(me GUI_ctxt: cr) <- {
        if (isHidden){return()}
        header.draw(cr)
        if(displayMode!=headerOnly){
            withEach element in elements:{
                element.isHidden <- false
                element.draw(cr)
            }
            if(orientation==horizontal){
                me int:lastElIdx <- elements.size()
                if(lastElIdx>=1){
                    me int: fromX <- posX+135
                    me int: fromY <- posY+12
                    me int: smallToX <- elements[0].posX
                    me int: largeToX <- elements[0].posX + elements[0].width
                    me int: smallToY <- elements[0].posY
                    me int: largeToY <- elements[0].posY + elements[0].height
                    our arrow:: Arrow(fromX, fromY, intersectPoint(fromX, smallToX, largeToX), intersectPoint(fromY, smallToY, largeToY))
                    dashBoard.decorations.pushLast(Arrow)

                    lastElIdx <- lastElIdx-1
                    cr.moveTo(elements[0].posX, elements[0].posY)
                    cr.lineTo(elements[lastElIdx].posX + elements[lastElIdx].width, elements[lastElIdx].posY)
                    cr.moveTo(elements[0].posX, elements[0].posY+18)
                    cr.lineTo(elements[lastElIdx].posX + elements[lastElIdx].width, elements[lastElIdx].posY+18)
                    cr.strokeNow()
                }
            }
        }
    }
}

struct GLOBAL{

    me int: intersectPoint(me int:outsidePt, me int:smallPt, me int: largePt) <- {
        me int: ret
        if(outsidePt<smallPt){ret<-smallPt}
        else if(outsidePt>largePt){ret<-largePt}
        else{ret<-outsidePt}
        return(ret)
    }
}
