// IOBuffer.Lib.dog

struct strInnerBuf{
    me string: buf
    me int64: myOffset
    me int64: refCount
    me int64: endingLineNumber
    their strInnerBuf: next
    their strInnerBuf: prev
}

struct strBuf{
    their strInnerBuf: head
    their strInnerBuf: tail
    me int64: crntStreamSize
    me Mutex: bufMutex
    me SyncLock: bufLock
    me bool: bufferClosed

    void: putStr(me string: data) <- { 
	// Part 1: count carriage returns and add it to previous one and assign to endingLineNumber
        log("PUTTING:"+data)
        me MutexMngr: bufMtxMgr{bufMutex}
        if(bufferClosed){
	    log("BUFF_CLOSED:"+data)
	    return()
	}
        their strInnerBuf:: innerBuf
        innerBuf.buf <- data
        innerBuf.myOffset <- crntStreamSize
        innerBuf.refCount <- 1
        crntStreamSize <+- data.size()
	me int: newLines <- 0
	me int: dataSize <- data.size()
	withEach idx in RANGE(0..dataSize){
            if(data[idx] == "\n"){
                newLines <+- 1
            }
        }
	// prev hasn't been set... how do I get prev?
	innerBuf.endingLineNumber <- innerBuf.prev.endingLineNumber + newLines
        if(tail==NULL){
	    head <- innerBuf
	    //innerBuf.endingLineNumber <- newLines
	} else {
	    tail.next <- innerBuf
	    innerBuf.prev <- tail
	    //innerBuf.endingLineNumber <- innerBuf.prev.endingLineNumber + newLines
	}
        tail <- innerBuf
        //log("NOTIFYING:")
	log("endingLineNumber of InnerBuf:"+toString(innerBuf.endingLineNumber))
        bufLock.notifyOne()
    }
    their strInnerBuf: findInrBufWithPos(me int64: pos) <- {
        //log("in FindInrBufWithPos(): "+ toString(pos))
        me MutexMngr: bufMtxMgr{bufMutex}
        while(tail==NULL or (tail.myOffset + tail.buf.size() <= pos)){
            if(bufferClosed){return(NULL)}
            //log("WAITING(findInrBufWithPos)")
            bufLock.wait(bufMtxMgr)
            //log("DONE_WAITING(findInrBufWithPos)")
        }
        their strInnerBuf: bufr <- tail
        while(bufr!=NULL){
            if(bufr.myOffset <= pos and bufr.myOffset + bufr.buf.size() > pos){
                return(bufr)
            }
            bufr <- bufr.prev
        }
        return(NULL)
    }
    me char: at(me int64: pos) <- {
        their strInnerBuf: crntInnerBuf <- findInrBufWithPos(pos)
        if(crntInnerBuf==NULL){return(0)}
        me char: retChar <- crntInnerBuf.buf[pos-crntInnerBuf.myOffset]
        return(retChar)
    }
    our strBufItr: getItrAt(me int64: pos) <- {
        their strInnerBuf: crntInnerBuf <- findInrBufWithPos(pos)
        if(crntInnerBuf==NULL){return(NULL)}
        our strBufItr:: retVal
        retVal.theStrBuf    <- self
        retVal.crntInnerBuf <- crntInnerBuf
        retVal.crntRelPos   <- pos - retVal.crntInnerBuf.myOffset
        retVal.status       <- bfOK
        return(retVal)
    }
    me string:atSafe(me int64: pos) <- {
        // Use this to print or log; Won't return 0 or block
        if(tail==NULL){return("BufEmpty")}
        if(tail.myOffset + tail.buf.size() <= pos){return("PastEOB")}
        their strInnerBuf: bufr <- tail
        while(bufr!=NULL){
            if(bufr.myOffset <= pos and bufr.myOffset + bufr.buf.size() > pos){
                me char: ch <- bufr.buf[pos-bufr.myOffset]
                if(ch=="\n"){return("\\n")}
                if(ch=="\t"){return("\\t")}
                if(ch<" "){me int: chNum<-ch; return("\\"+toString(chNum))}
                me string: S; S <+- ch
                return(S)
            }
            bufr <- bufr.prev
        }
        return("BufERROR")
    }
    void: close() <- {
        me MutexMngr: bufMtxMgr{bufMutex}
        bufferClosed <- true
        bufLock.notifyOne()
        //log("CLOSE-NOTIFYING:")
    }
    me int64: size() <- {
	return(crntStreamSize)
    }
    me string: stringify() <- {
        me string: retVal
        their strInnerBuf: crnt <- head
        while(crnt!=NULL){
            retVal <- "'"+crnt.buf + "'\n"
            crnt <- crnt.next
        }
        return(retVal)
    }
    void: init() <- {
        head <- NULL
        tail <- NULL
        crntStreamSize <- 0
        bufferClosed   <- false
    }
}

struct strBufItr{
    their strBuf: theStrBuf
    their strInnerBuf: crntInnerBuf
    me int64: crntRelPos
    me int64: crntAbsPos() <- {return(crntInnerBuf.myOffset + crntRelPos)}
    me mode[bfWaiting, bfOK, bfEOB, bfError]: status
    me char: ch() <- {
        return(crntInnerBuf.buf[crntRelPos])
    }
    me int: goNext() <- {
        crntRelPos <+- 1
        if(crntRelPos >= crntInnerBuf.buf.size()){
            crntInnerBuf <- theStrBuf.findInrBufWithPos(crntAbsPos())
            if(crntInnerBuf==NULL){
                if(theStrBuf.bufferClosed){status <- bfEOB}
                else{status <- bfError}
            }else{
                crntRelPos <- 0
                status <- bfOK
            }
        }
        return(status)
    }
    me string: stringify() <- {
        me string: retVal
        if(crntInnerBuf){retVal <- "'"+crntInnerBuf.buf +"'("+toString(crntRelPos)+ ")\n"}
        else{retVal <- "NULL"}
        return(retVal)
    }
}

struct strBufRange{
    their strBuf: theStrBuf
    me int64: startPos
    me int64: endPos
    void: INIT(me int64: start, me int64: end) <- {}  // inc RefCount
    void: DEINIT() <- {} // dec RefCOunt
}

