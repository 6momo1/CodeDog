// IOBuffer.Lib.dog

struct strInnerBuf{
    me string: buf
    me int64: myOffset
    me int64: refCount
    me int64: endingLineNumber
    their strInnerBuf: next
    their strInnerBuf: prev
}

struct strBuf{
    their strInnerBuf: head
    their strInnerBuf: tail
    me int64: crntStreamSize
    me Mutex: bufMutex
    me bool: bufferClosed

    void: putStr(their string: data) <- {
        if(bufferClosed){return()}
        their strInnerBuf:: innerBuf
        innerBuf.buf <- data
        innerBuf.myOffset <- crntStreamSize
        innerBuf.refCount <- 1
        crntStreamSize <+- data.size()
        bufMutex.lock()
        if(tail==NULL){ head <- innerBuf}
        else{tail.next <- innerBuf}
        tail <- innerBuf
        bufMutex.unlock()
    }
    their strInnerBuf: findInrBufWithPos(me int64: pos) <- {
        bufMutex.lock()
        their strInnerBuf: bufr <- head
        while(bufr!=NULL){
            if(bufr.myOffset <= pos and bufr.myOffset + bufr.buf.size() > pos){
                bufMutex.unlock()
                return(bufr)
            }
            bufr <- bufr.next
        }
        bufMutex.unlock()
        return(NULL)
    }
    me char: at(me int64: pos) <- {
        their strInnerBuf: crntInnerBuf <- findInrBufWithPos(pos)
        if(crntInnerBuf==NULL){return(0)}
        me char: retChar <- crntInnerBuf.buf[pos-crntInnerBuf.myOffset]
        return(retChar)
    }
    our strBufItr: getItrAt(me int64: pos) <- {
        their strInnerBuf: crntInnerBuf <- findInrBufWithPos(pos)
        if(crntInnerBuf==NULL){return(NULL)}
        our strBufItr:: retVal
        retVal.theStrBuf    <- self
        retVal.crntInnerBuf <- crntInnerBuf
        retVal.crntRelPos   <- pos - retVal.crntInnerBuf.myOffset
        retVal.status       <- bfOK
        return(retVal)
    }
    void: close() <- {
        bufferClosed <- true
    }
    me int64: size() <- {return(crntStreamSize)}
    me string: stringify() <- {
        me string: retVal
        their strInnerBuf: crnt <- head
        while(crnt!=NULL){
            retVal <- "'"+crnt.buf + "'\n"
            crnt <- crnt.next
        }
        return(retVal)
    }
    void: init() <- {
        head <- NULL
        tail <- NULL
        crntStreamSize <- 0
        bufferClosed   <- false
    }
}

struct strBufItr{
    their strBuf: theStrBuf
    their strInnerBuf: crntInnerBuf
    me int64: crntRelPos
    me int64: crntAbsPos() <- {return(crntInnerBuf.myOffset + crntRelPos)}
    me mode[bfWaiting, bfOK, bfEOB, bfError]: status
    me char: ch() <- {
        return(crntInnerBuf.buf[crntRelPos])
    }
    me int: goNext() <- {
        crntRelPos <+- 1
        if(crntRelPos >= crntInnerBuf.buf.size()){
            crntRelPos <- 0
            crntInnerBuf <- crntInnerBuf.next
            if(crntInnerBuf==NULL){
                if(theStrBuf.bufferClosed){
                    status <- bfEOB
                }else{ // TODO: wait for buffer to fill
                    //theStrBuf.bufMutex.lock()
                    status <- bfWaiting
                }
            }else{status <- bfOK}
        }
        return(status)
    }
    me string: stringify() <- {
        me string: retVal
        if(crntInnerBuf){retVal <- "'"+crntInnerBuf.buf +"'("+toString(crntRelPos)+ ")\n"}
        else{retVal <- "NULL"}
        return(retVal)
    }
}

struct strBufRange{
    their strBuf: theStrBuf
    me int64: startPos
    me int64: endPos
    void: INIT(me int64: start, me int64: end) <- {}  // inc RefCount
    void: DEINIT() <- {} // dec RefCOunt
}

